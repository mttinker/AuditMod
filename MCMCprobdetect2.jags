model{
# Logit model for observed detections
for (j in 1:Nobs){
	y[j] ~ dbern(y.hat[j])	
    y.hat[j] <- max(0, min(1,P[obs[j]]))
}	
for (i in 1:Nsamp){	
	lgDNN[i] ~ dnorm(ElgDNN[i],tauDNN)
	# lgDNN[i] ~ dnorm(ElgDNN[i],tau[site[i]])
	ElgDNN[i] <- alpha[site[i]] + phi[site[i]]*P[i] + inprod(B, X[i,])
	P[i] <- ilogit(lgP[i]) # True Prob of call detection this sample: latent var
	lgP[i] ~ dnorm(lgPs[site[i]],tauS[site[i]]) # Uniform prior: logit(P[i]) for this 2s sample
}
# Priors
# Fixed effect params for signal quality variables , X_j
for (b in 1:Neff){
    B[b] ~ dt(0, tauB[b], 1) # cauchy prior (Gelman et al., 2008)
}
# Site-specific effects for intercept and slope of DNNprob
for (s in 1:Nsite){
	alpha[s] ~ dnorm(alph0,tauA) T(-30,30)
	phi[s] ~ dnorm(phi0,tauH) T(0,50)
	lgPs[s] ~ dnorm(lgP0,tauP) T(-12,12)
	sigS[s] ~ dt(0, 1/7^2 ,1) T(0,20) # half cauchy(0, 2.5) prior (Gelman et al., 2008)
	tauS[s] <- pow(sigS[s],-2) 
	# 
}
tauDNN <- 1/.25^2
# HyperPriors for intcpt and slope of fxn "Machine Learning" DNNprob ~ TrueProb 
alph0 ~ dnorm(0,.001) # 
phi0 ~ dnorm(0,.001) # 
lgP0 ~ dnorm(0,.001) # 
# Variance params for site effects (and/or acoustic instrument)
tauP <- pow(sigP,-2) 
sigP ~ dt(0, 1/2.5^2 ,1) T(0,5) # half cauchy(0, 1) prior (Gelman et al., 2008)
tauA <- pow(sigA,-2) 
sigA ~ dt(0, 1/2.5^2 ,1) T(0,10) # half cauchy(0, 2.5) prior (Gelman et al., 2008)
tauH <- pow(sigH,-2) 
sigH ~ dt(0, 1/2.5^2 ,1) T(0,5) # half cauchy(0, 2.5) prior (Gelman et al., 2008)
}